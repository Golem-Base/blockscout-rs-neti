default:
    just --list --unsorted

db-host := env_var_or_default('DB_HOST', "localhost")
db-port := env_var_or_default('DB_PORT', "7432")
db-user := env_var_or_default('DB_USER', "blockscout")
db-password := env_var_or_default('DB_PASSWORD', "ceWb1MeLBEeOIfk65gU8EjF8")
db-name := env_var_or_default('DB_NAME', "blockscout")
export DATABASE_URL := "postgres://" + db-user + ":" + db-password + "@" + db-host + ":" + db-port + "/" + db-name

test *args:
  - docker stop golem-base-indexer-postgres
  docker run -p 8432 --name golem-base-indexer-postgres -e POSTGRES_PASSWORD=admin -e POSTGRES_USER=admin --rm -d postgres -N 500
  env DATABASE_URL=$(dirname $DATABASE_URL) cargo test {{args}} -- --include-ignored
  docker stop golem-base-indexer-postgres

run:
    GOLEM_BASE_INDEXER__DATABASE__CONNECT__URL={{DATABASE_URL}} \
    GOLEM_BASE_INDEXER__DATABASE__RUN_MIGRATIONS=true \
    cargo run --bin golem-base-indexer-server

generate-entities:
    sea-orm-cli generate entity --lib -o golem-base-indexer-entity/src

new-migration name:
    sea-orm-cli migrate generate -d golem-base-indexer-migration {{name}}

migrate-up:
    sea-orm-cli migrate -d golem-base-indexer-migration up

migrate-down:
    sea-orm-cli migrate -d golem-base-indexer-migration down
